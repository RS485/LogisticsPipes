buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
//apply plugin: 'curseforge'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

//ant.importBuild 'download-api.xml'
ant.importBuild 'build-ant.xml'

version = "0.10.1"
group= "network.rs485.logisticspipes" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "logisticspipes"

def buildby = 'local'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version += "." + ENV.BUILD_NUMBER
    buildby = 'RS485-Jenkins'
} else {
    version += ".99999"
}

ant.version = version
ant.archivesBaseName = archivesBaseName

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run/assets"
    replace '%VERSION%', project.version
    replace '%MCVERSION%', project.minecraft.version
    replace '%DEBUG%', "false"
    replaceIn "LPConstants.java"
    mappings = "snapshot_20171120"
}

jar {
    manifest {
        attributes('FMLCorePlugin': 'logisticspipes.asm.LogisticsPipesCoreLoader',
                   'FMLCorePluginContainsFMLMod': 'true',
                   'Implementation-Vendor': 'RS485',
                   'Implementation-Title': 'LogisticsPipes',
                   'Implementation-Version': version,
                   'Built-By': buildby )
    }
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir 'common'
        }
        resources {
            srcDir 'resources'
        }
    }
    api {
        java {
            srcDir 'dummy'
            srcDir 'api'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

test {
    reports {
        html.enabled = true
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        name = 'CB Repo'
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = "JEI Maven."
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "player"
        url = "http://maven.ic2.player.to"
    }
    maven {
        url = "https://cc.crzd.me/maven/"
    }
    maven {
        url = "http://maven.cil.li/"
    }
    maven {
        name = "buildcraft"
        url = "https://mod-buildcraft.com/maven/"
    }
    maven { // JEI Bees & Thaumcraft
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name = "buildcraft"
        url = "https://mod-buildcraft.com/maven"
    }
}

dependencies {
    deobfCompile "com.mod-buildcraft:buildcraft:7.99.17"
    compileOnly 'org.projectlombok:lombok:1.16.8'
    compile 'codechicken:NotEnoughItems:1.12.2-2.4.1.233:deobf'
    compile 'codechicken:CodeChickenLib:1.12.2-3.1.7.340:deobf'
    compile 'codechicken:EnderStorage:1.12.2-2.4.2.126:deobf'
    compile 'net.industrial-craft:industrialcraft-2:2.8.97-ex112:api'
    compile 'net.sengir.forestry:forestry_1.12.2:5.8.1.339:api'
    compile "li.cil.oc:OpenComputers:MC1.12.2-1.7.2.+:api"
    compile "appeng:appliedenergistics2:rv5-stable-11:api"
    compileOnly "dan200.computercraft:ComputerCraft:1.80pr1-build5"
    deobfCompile "thaumcraft:Thaumcraft:1.12.2:6.1.BETA26"
    runtimeOnly "baubles:Baubles:1.12:1.5.2"
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
}

task deobfJar(type: Jar, dependsOn: 'jar') {
    from sourceSets.main.output
    classifier = 'dev'
    manifest {
        attributes(
            'FMLCorePlugin': 'logisticspipes.asm.LogisticsPipesCoreLoader',
            'FMLCorePluginContainsFMLMod': 'true',
            'Implementation-Vendor': 'RS485',
            'Implementation-Title': 'LogisticsPipes',
            'Implementation-Version': version,
            'Built-By': buildby)
    }
}

//task sourceJar(type: Jar, dependsOn: 'jar') {
//    from sourceSets.main.allSource
//    classifier = 'src'
//}

//curse {
//    displayName = 'Logistics Pipes 0.10.0 (BC7) build ' + System.getenv().BUILD_NUMBER
//
//    projectId = '232838'
//    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''
//    releaseType = 'release'
//
//    changelog = project.hasProperty('changelog') ? project.changelog : ''
//}

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
}

//tasks.compileApiJava.dependsOn('extract')
tasks.compileJava.dependsOn('loadFingerprints')
tasks.processResources.dependsOn('integrateLang')
tasks.assemble.dependsOn('deobfJar', 'sourceJar', 'signJars')
